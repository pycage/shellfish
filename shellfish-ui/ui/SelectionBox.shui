/*******************************************************************************
This file is part of the Shellfish UI toolkit.
Copyright (c) 2020 - 2024 Martin Grimme <martin.grimme@gmail.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*******************************************************************************/

require "shellfish/ui";

/**
 * Element representing a drop-down selection box.
 *
 * @example
 * SelectionBox {
 *     model: ListModel { data: ["One", "Two", "Three"] }
 * }
 *
 * @memberof ui
 * @name SelectionBox
 * @class
 * @extends ui.Button
 *
 * @property {string} emptyText - (default: `""`) The text to show if the model is empty.
 * @property {html.ListModel} model - (default: `ListModel { }`) The list model of strings holding the selection items.
 * @property {number} selection - (default: `0`) The index of the currently selected item.
 */
Button {
    id: box

    property model: ListModel { }
    property selection: 0
    property emptyText: "?"

    property _menu: null

    function _createMenu()
    {
        _menu = _menuT();
    }

    minWidth: theme.itemWidthLarge

    secondaryIcon: "core-menu"
    text: model.size > selection ? model.at(selection) : emptyText

    onPointerDown: ev =>
    {
        click(ev);
    }

    onClick: ev =>
    {
        ev.accepted = true;

        if (! _menu)
        {
            _createMenu();
        }

        _menu.popup(self);
    }

    onWheel: (ev) =>
    {
        ev.accepted = true;

        if (ev.deltaY < 0 && selection > 0)
        {
            --selection;
        }
        else if (ev.deltaY > 0 && selection < model.size - 1)
        {
            ++selection;
        }
    }

    onKeyDown: (ev) =>
    {
        if (ev.key === "ArrowUp")
        {
            if (selection > 0)
            {
                --selection;
            }
            ev.accepted = true;
        }
        else if (ev.key === "ArrowDown")
        {
            if (selection < model.size - 1)
            {
                ++selection;
            }
            ev.accepted = true;
        }
    }

    property _menuT: template Menu {

        onCascadingClose: () =>
        {
            // give focus back to the button when the menu closes
            box.focus = true;
        }

        ListView {
            width: thisMenu.menuRuler.max.width
            height: Math.min(cellHeight * count, cellHeight * 10, thisDocument.windowHeight * 0.8)
            cellWidth: bboxWidth
            cellHeight: theme.itemHeightMedium
            canFocus: true
           
            model: box.model

            onAncestorsVisibleChanged: () =>
            {
                if (ancestorsVisible)
                {
                    wait(100).then(() =>
                    {
                        positionViewAt(box.selection);
                        selector.select(box.selection, "replace");
                    });
                }
            }

            delegate: template MenuItem {
                property itemSelected: selector.selection.has(modelData.index)

                text: modelData.value

                onItemSelectedChanged: () =>
                {
                    if (itemSelected)
                    {
                        selectItem(itemSelected);
                    }
                }

                onClick: ev =>
                {
                    selector.select(modelData.index, "replace");
                    box.selection = modelData.index;
                }
            }

            ListViewSelector { id: selector }
            ScrollIndicator { }
        }
    }
}